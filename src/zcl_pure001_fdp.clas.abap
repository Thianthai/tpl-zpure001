CLASS zcl_pure001_fdp DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_rap_query_provider .

  PROTECTED SECTION.
  PRIVATE SECTION.

    TYPES:
    "! ผลลัพธ์ของแต่ละ entity ที่ส่งกลับให้ framework
      tt_header TYPE STANDARD TABLE OF zr_pure001_fdp      WITH EMPTY KEY,
      tt_item   TYPE STANDARD TABLE OF zi_pure001_item_fdp WITH EMPTY KEY,
      tt_text   TYPE STANDARD TABLE OF zi_pure001_itxt_fdp WITH EMPTY KEY.

    CONSTANTS:
    "! ชื่อ entity ที่ framework ส่งมาใน get_entity_id
    "! ประเภท long text ที่ฟอร์มใช้
      BEGIN OF gc_entity,
        header TYPE string VALUE 'ZR_PURE001_FDP',
        item   TYPE string VALUE 'ZI_PURE001_ITEM_FDP',
        text   TYPE string VALUE 'ZI_PURE001_ITXT_FDP',
      END OF gc_entity,

      BEGIN OF gc_text_type,
        material_po_text TYPE c LENGTH 4 VALUE 'F03',
        item_text        TYPE c LENGTH 4 VALUE 'F01',
        header_text      TYPE c LENGTH 4 VALUE 'F01',
        header_note      TYPE c LENGTH 4 VALUE 'F02',
        shipping_instr   TYPE c LENGTH 4 VALUE 'F06',
      END OF gc_text_type.

    CONSTANTS:
    "! หน่วยที่พิมพ์แทนเมื่อ item เป็นบริการ ตามฟอร์มมาตรฐาน
    "! ข้อความที่พิมพ์เมื่อใบสั่งซื้อผ่านการอนุมัติแล้ว
      gc_service_unit  TYPE c LENGTH 3 VALUE 'AU',
      gc_approval_note TYPE string
        VALUE 'เอกสารสั่งซื้อนี้ได้รับการอนุมัติจากผู้มีอำนาจ ผ่านระบบอิเล็กทรอนิกส์เรียบร้อยแล้ว'.

    DATA:
      go_data           TYPE REF TO zcl_pure001_data,
      gr_purchase_order TYPE RANGE OF zr_pure001_fdp-PurchaseOrder,
      gt_header         TYPE zcl_pure001_data=>tt_header,
      gt_item           TYPE zcl_pure001_data=>tt_item,
      gt_schedule_line  TYPE zcl_pure001_data=>tt_schedule_line,
      gt_account_assgmt TYPE zcl_pure001_data=>tt_account_assignment,
      gt_header_text    TYPE zcl_pure001_data=>tt_text,
      gt_item_text      TYPE zcl_pure001_data=>tt_text,
      gt_form_detail    TYPE zcl_pure001_data=>tt_form_detail.

    "! อ่านเลขที่ใบสั่งซื้อจาก filter ของ request
    "! ไม่มีเลขที่ใบสั่งซื้อจะ raise exception เพราะฟอร์มต้องมี key เสมอ
    METHODS prepare_filter
      IMPORTING io_request TYPE REF TO if_rap_query_request
      RAISING   cx_rap_query_provider.

    "! เดิน filter tree เก็บค่า PurchaseOrder จากทุก node "PurchaseOrder = value"
    "! ใช้เมื่อ filter แปลงเป็น range ไม่ได้ (child ที่มี key หลาย field)
    METHODS collect_po_from_tree
      IMPORTING io_node TYPE REF TO if_rap_query_filter_tree_node.

    "! อ่านข้อมูลทุกชุดของใบสั่งซื้อที่ขอครั้งเดียวผ่าน ZCL_PURE001_DATA
    METHODS load_data.

    "! ประกอบข้อมูลระดับ header หนึ่งบรรทัดต่อหนึ่งใบสั่งซื้อ
    METHODS build_headers
      RETURNING VALUE(rt_header) TYPE tt_header.

    "! ประกอบข้อมูลระดับ item เฉพาะ item ที่ไม่ถูกลบ
    METHODS build_items
      RETURNING VALUE(rt_item) TYPE tt_item.

    "! ประกอบ long text ของ item แยกหนึ่งบรรทัดต่อหนึ่งประเภท text
    METHODS build_item_texts
      RETURNING VALUE(rt_text) TYPE tt_text.

    "! item ที่ไม่ถูกลบของใบสั่งซื้อ เรียงตาม item
    METHODS get_active_items
      IMPORTING iv_purchase_order TYPE zr_pure001_fdp-PurchaseOrder
      RETURNING VALUE(rt_item)    TYPE zcl_pure001_data=>tt_item.

    "! long text ของ header ตามภาษาและประเภทที่ระบุ ว่างเมื่อไม่พบ
    METHODS get_header_text
      IMPORTING iv_purchase_order TYPE zr_pure001_fdp-PurchaseOrder
                iv_language       TYPE zr_pure001_fdp-Language
                iv_text_type      TYPE zcl_pure001_data=>ty_text-TextObjectType
      RETURNING VALUE(rv_text)    TYPE string.

    "! text ของ item เรียงตามฟอร์ม คือ F03 แล้วตามด้วย F01
    "! ใช้ภาษาของใบสั่งซื้อ และเอาเฉพาะ text ที่มีข้อความ
    METHODS get_ordered_item_texts
      IMPORTING iv_purchase_order      TYPE clike
                iv_purchase_order_item TYPE clike
                iv_language            TYPE clike
      RETURNING VALUE(rt_text)         TYPE zcl_pure001_data=>tt_text.

    "! ข้อความทั้งช่องรายการ คั่นแต่ละบรรทัดด้วย newline
    "! เรียงเป็น คำอธิบาย, long text, วันส่งของ, รหัสวัสดุ, PR/Acc/Order และ WBS
    METHODS compose_item_description
      IMPORTING is_item        TYPE zi_pure001_item_fdp
                iv_language    TYPE zr_pure001_fdp-Language
      RETURNING VALUE(rv_text) TYPE string.

ENDCLASS.



CLASS ZCL_PURE001_FDP IMPLEMENTATION.

  METHOD if_rap_query_provider~select.

    go_data = NEW #( ).
    prepare_filter( io_request ).
    load_data( ).

    CASE io_request->get_entity_id( ).

      WHEN gc_entity-header.
        DATA(lt_header) = build_headers( ).

        IF io_request->is_total_numb_of_rec_requested( ).
          io_response->set_total_number_of_records( lines( lt_header ) ).
        ENDIF.

        IF io_request->is_data_requested( ).
          io_response->set_data( lt_header ).
        ENDIF.

      WHEN gc_entity-item.
        DATA(lt_item) = build_items( ).

        IF io_request->is_total_numb_of_rec_requested( ).
          io_response->set_total_number_of_records( lines( lt_item ) ).
        ENDIF.

        IF io_request->is_data_requested( ).
          io_response->set_data( lt_item ).
        ENDIF.

      WHEN gc_entity-text.
        DATA(lt_text) = build_item_texts( ).

        IF io_request->is_total_numb_of_rec_requested( ).
          io_response->set_total_number_of_records( lines( lt_text ) ).
        ENDIF.

        IF io_request->is_data_requested( ).
          io_response->set_data( lt_text ).
        ENDIF.

    ENDCASE.

  ENDMETHOD.


  METHOD prepare_filter.

    CLEAR gr_purchase_order.

    TRY.
        " root / item: framework ส่ง PurchaseOrder = x (หรือ OR หลายค่า) -> range ได้
        DATA(lt_filter) = io_request->get_filter( )->get_as_ranges( ).

        ASSIGN lt_filter[ name = 'PURCHASEORDER' ] TO FIELD-SYMBOL(<lfs_filter>).
        IF sy-subrc = 0.
          gr_purchase_order = CORRESPONDING #( <lfs_filter>-range ).
        ENDIF.

      CATCH cx_rap_query_filter_no_range.
        " item text มี key สองส่วน framework จึงส่ง (PO = x AND Item = y) OR (PO = x AND Item = z)
        " range แยกต่อ field ผูกคู่กันไม่ได้ จึงเดิน filter tree เก็บเฉพาะเลขที่ใบสั่งซื้อ
        " ฟอร์มต้องการแค่รายการใบสั่งซื้อ เพราะเราส่ง text ของ item ที่ไม่ถูกลบกลับไปทั้งหมดอยู่แล้ว
        DATA(lo_tree) = io_request->get_filter( )->get_as_tree( ).
        IF lo_tree IS BOUND.
          collect_po_from_tree( lo_tree->get_root_node( ) ).
        ENDIF.

        SORT gr_purchase_order BY low.
        DELETE ADJACENT DUPLICATES FROM gr_purchase_order COMPARING low.

    ENDTRY.

    IF gr_purchase_order IS INITIAL.
      RAISE EXCEPTION NEW zcx_pure001_query( text = 'Purchase order key is required for form data' ).
    ENDIF.

  ENDMETHOD.


  METHOD collect_po_from_tree.

    DATA lo_child      TYPE REF TO if_rap_query_filter_tree_node.
    DATA lo_identifier TYPE REF TO data.
    DATA lo_value      TYPE REF TO data.
    DATA lv_identifier TYPE string.

    CASE io_node->get_type( ).

      WHEN if_rap_query_filter_tree_types=>node_types-identifier
        OR if_rap_query_filter_tree_types=>node_types-value.
        " leaf ถูกอ่านไปแล้วตอนอยู่ที่ node equals ด้านบน
        RETURN.

      WHEN if_rap_query_filter_tree_types=>node_types-equals.
        " node นี้มีลูกสองตัวแต่ไม่การันตีลำดับ จึงแยก identifier กับ value เอง
        LOOP AT io_node->get_children( ) INTO lo_child.
          CASE lo_child->get_type( ).
            WHEN if_rap_query_filter_tree_types=>node_types-identifier.
              lo_identifier = lo_child->get_value( ).
              ASSIGN lo_identifier->* TO FIELD-SYMBOL(<lfs_identifier>).
              " เอกสารของ SAP ไม่การันตีตัวพิมพ์ของชื่อ field
              lv_identifier = to_upper( <lfs_identifier> ).

            WHEN if_rap_query_filter_tree_types=>node_types-value.
              lo_value = lo_child->get_value( ).

          ENDCASE.
        ENDLOOP.

        IF lv_identifier = 'PURCHASEORDER' AND lo_value IS BOUND.
          ASSIGN lo_value->* TO FIELD-SYMBOL(<lfs_value>).
          APPEND VALUE #( sign = 'I' option = 'EQ' low = <lfs_value> ) TO gr_purchase_order.
        ENDIF.

      WHEN OTHERS.
        " node แบบ AND หรือ OR ให้ลงไปดูลูกทุกตัว
        " node ของ PurchaseOrderItem จะถูกข้ามไปเองที่เงื่อนไขชื่อ field ด้านบน
        LOOP AT io_node->get_children( ) INTO lo_child.
          collect_po_from_tree( lo_child ).
        ENDLOOP.

    ENDCASE.

  ENDMETHOD.


  METHOD load_data.

    gt_header = go_data->read_headers( VALUE #( purchase_order = gr_purchase_order ) ).
    IF gt_header IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lt_po_key) = VALUE zcl_pure001_data=>tt_po_key( FOR ls_h IN gt_header ( PurchaseOrder = ls_h-PurchaseOrder ) ).

    gt_item           = go_data->read_items( lt_po_key ).
    gt_schedule_line  = go_data->read_schedule_lines( lt_po_key ).
    gt_account_assgmt = go_data->read_account_assignments( lt_po_key ).
    gt_header_text    = go_data->read_header_texts( lt_po_key ).
    gt_item_text      = go_data->read_item_texts( lt_po_key ).
    gt_form_detail    = go_data->read_form_details( gt_header ).

  ENDMETHOD.


  METHOD build_headers.

    LOOP AT gt_header ASSIGNING FIELD-SYMBOL(<lfs_src>).

      DATA(lt_active_item) = get_active_items( <lfs_src>-PurchaseOrder ).
      DATA(ls_first_item)  = VALUE #( lt_active_item[ 1 ] OPTIONAL ).

      APPEND INITIAL LINE TO rt_header ASSIGNING FIELD-SYMBOL(<lfs_out>).

      "--- Company ---------------------------------------------------------------
      <lfs_out>-PurchaseOrder    = <lfs_src>-PurchaseOrder.
      <lfs_out>-CompanyCode      = <lfs_src>-CompanyCode.
      <lfs_out>-CompanyName      = <lfs_src>-CompanyCodeName.
      <lfs_out>-CompanyTaxNumber = <lfs_src>-CompanyTaxNumber.
      " ที่อยู่ โทรศัพท์ เว็บไซต์ และโลโก้ของบริษัทพิมพ์เป็นค่าคงที่ในฟอร์ม

      "--- PO --------------------------------------------------------------------
      <lfs_out>-PurchaseOrderType     = <lfs_src>-PurchaseOrderType.
      <lfs_out>-PurchaseOrderTypeName = <lfs_src>-PurchaseOrderTypeName.
      <lfs_out>-Language              = <lfs_src>-Language.
      <lfs_out>-PurchaseOrderDate     = <lfs_src>-PurchaseOrderDate.
      <lfs_out>-PurchaseOrderDateText = zcl_pure001_util=>to_thai_date( <lfs_src>-PurchaseOrderDate ).
      <lfs_out>-ExternalReference     = <lfs_src>-ExternalReference.
      <lfs_out>-InternalReference     = <lfs_src>-InternalReference.
      <lfs_out>-PaymentTerms          = <lfs_src>-PaymentTerms.
      <lfs_out>-PaymentTermsText      = <lfs_src>-PaymentTermsText.
      <lfs_out>-ValidityStartDate     = <lfs_src>-ValidityStartDate.
      <lfs_out>-ValidityStartDateText = zcl_pure001_util=>to_thai_date( <lfs_src>-ValidityStartDate ).
      <lfs_out>-ValidityEndDate       = <lfs_src>-ValidityEndDate.
      <lfs_out>-ValidityEndDateText   = zcl_pure001_util=>to_thai_date( <lfs_src>-ValidityEndDate ).

      " วันที่ส่งสินค้าระดับ header ใช้ schedule line ของ item แรก
      <lfs_out>-DeliveryDate = VALUE #( gt_schedule_line[ PurchaseOrder     = ls_first_item-PurchaseOrder
                                                          PurchaseOrderItem = ls_first_item-PurchaseOrderItem ]-ScheduleLineDeliveryDate OPTIONAL ).
      <lfs_out>-DeliveryDateText = zcl_pure001_util=>to_thai_date( <lfs_out>-DeliveryDate ).

      <lfs_out>-ShipVia    = get_header_text( iv_purchase_order = <lfs_src>-PurchaseOrder iv_language = <lfs_src>-Language iv_text_type = gc_text_type-shipping_instr ).
      <lfs_out>-HeaderText = get_header_text( iv_purchase_order = <lfs_src>-PurchaseOrder iv_language = <lfs_src>-Language iv_text_type = gc_text_type-header_text ).
      <lfs_out>-HeaderNote = get_header_text( iv_purchase_order = <lfs_src>-PurchaseOrder iv_language = <lfs_src>-Language iv_text_type = gc_text_type-header_note ).

      " ค่าที่ฟอร์มมาตรฐานได้จาก BAdI เราอ่านผ่าน custom class ชุดเดียวกัน
      ASSIGN gt_form_detail[ PurchaseOrder = <lfs_src>-PurchaseOrder ] TO FIELD-SYMBOL(<lfs_detail>).
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.

      " วันที่ออกเอกสารใช้วันที่แก้ไขล่าสุดตามฟอร์มมาตรฐาน
      <lfs_out>-PurchaseOrderDateText = <lfs_detail>-IssuedDateText.

      " วันที่สัญญาอ่านจาก custom field ของใบสั่งซื้อ
      <lfs_out>-ValidityStartDate     = <lfs_src>-FrameworkStartDate.
      <lfs_out>-ValidityStartDateText = zcl_pure001_util=>to_thai_date( <lfs_src>-FrameworkStartDate ).
      <lfs_out>-ValidityEndDate       = <lfs_src>-FrameworkEndDate.
      <lfs_out>-ValidityEndDateText   = zcl_pure001_util=>to_thai_date( <lfs_src>-FrameworkEndDate ).

      " ช่องหลักประกันเป็น custom field ที่ผู้ใช้ติ๊กบนใบสั่งซื้อ
      <lfs_out>-PerfGuaranteeFlag     = <lfs_src>-PerformanceBondFlag.
      <lfs_out>-PerfGuaranteeCash     = <lfs_src>-PerformanceBondCash.
      <lfs_out>-PerfGuaranteeBG       = <lfs_src>-PerformanceBondBG.
      <lfs_out>-InsurancePolicyFlag   = <lfs_src>-InsuranceFlag.
      <lfs_out>-WarrantyGuaranteeFlag = <lfs_src>-WarrantyBondFlag.
      <lfs_out>-WarrantyGuaranteeCash = <lfs_src>-WarrantyBondCash.
      <lfs_out>-WarrantyGuaranteeBG   = <lfs_src>-WarrantyBondBG.

      "--- Supplier --------------------------------------------------------------
      <lfs_out>-Supplier           = <lfs_src>-Supplier.
      <lfs_out>-SupplierName       = <lfs_src>-SupplierName.
      <lfs_out>-SupplierCodeName   = <lfs_detail>-SupplierCodeName.
      <lfs_out>-SupplierTaxNumber  = <lfs_src>-SupplierTaxNumber.
      <lfs_out>-SupplierStreet     = <lfs_src>-SupplierStreet.
      <lfs_out>-SupplierDistrict   = <lfs_src>-SupplierDistrict.
      <lfs_out>-SupplierCity       = <lfs_src>-SupplierCity.
      <lfs_out>-SupplierPostalCode = <lfs_src>-SupplierPostalCode.
      <lfs_out>-SupplierAddress    = <lfs_detail>-SupplierAddress.
      <lfs_out>-SupplierContactName = <lfs_src>-SupplierRespSalesPersonName.
      <lfs_out>-SupplierPhone       = COND #( WHEN <lfs_src>-SupplierPhoneNumber IS NOT INITIAL
                                              THEN <lfs_src>-SupplierPhoneNumber
                                              ELSE <lfs_src>-SupplierMasterPhone ).
      <lfs_out>-SupplierEmail       = <lfs_src>-SupplierEmail.

      "--- Ship-to ---------------------------------------------------------------
      <lfs_out>-ShipToPlant     = ls_first_item-Plant.
      <lfs_out>-ShipToPlantName = ls_first_item-PlantName.

      " สถานที่จัดส่งใช้ชื่อและที่อยู่ของ plant ตามฟอร์มมาตรฐาน
      " ชื่อภาษาไทยอยู่บรรทัดแรก ที่อยู่อยู่บรรทัดถัดมา
      DATA(ls_ship_to) = zcl_pure001_address=>get_by_plant( ls_first_item-Plant ).

      <lfs_out>-ShipToName         = COND #( WHEN ls_ship_to-Name2 IS NOT INITIAL
                                             THEN ls_ship_to-Name2
                                             ELSE ls_ship_to-Name1 ).
      <lfs_out>-ShipToAddressLine1 = ls_ship_to-AddressText.

      <lfs_out>-GoodsRecipientName  = <lfs_detail>-RecipientContact.
      <lfs_out>-GoodsRecipientPhone = <lfs_detail>-RecipientTelephone.
      ASSIGN gt_account_assgmt[ PurchaseOrder     = ls_first_item-PurchaseOrder
                                PurchaseOrderItem = ls_first_item-PurchaseOrderItem ] TO FIELD-SYMBOL(<lfs_first_aa>).
      IF sy-subrc = 0.
        <lfs_out>-UnloadingPointName = <lfs_first_aa>-UnloadingPointName.
      ENDIF.

      "--- Totals — ใช้ยอดชุดเดียวกับฟอร์มมาตรฐาน ------------------------------------
      <lfs_out>-DocumentCurrency = <lfs_src>-DocumentCurrency.
      <lfs_out>-TotalAmount      = <lfs_detail>-SumNetAmount.
      <lfs_out>-DiscountAmount   = <lfs_detail>-SumOtherExpense.
      <lfs_out>-AmountBeforeTax  = <lfs_detail>-SumAmount.
      <lfs_out>-TaxAmount        = <lfs_detail>-SumTax.
      <lfs_out>-NetAmount        = <lfs_detail>-SumTotalNetAmount.
      <lfs_out>-AmountInWords    = zcl_pure001_util=>amount_in_words( iv_amount   = <lfs_out>-NetAmount
                                                                      iv_currency = <lfs_out>-DocumentCurrency ).

      "--- Signature -------------------------------------------------------------
      <lfs_out>-PreparedByUser   = <lfs_src>-CreatedByUser.
      <lfs_out>-PreparedByName   = <lfs_detail>-PreparedByName.
      <lfs_out>-PreparedDate     = <lfs_src>-CreationDate.
      <lfs_out>-PreparedDateText = <lfs_detail>-IssuedDateText.

      " วันที่อนุมัติว่างหมายถึงใบนี้ยังไม่ผ่านการอนุมัติ
      " ใช้ค่าดิบเพราะข้อความที่แปลงแล้วจะเป็นเส้นประเมื่อยังไม่อนุมัติ
      DATA(lv_approved) = xsdbool( <lfs_detail>-ApprovalDateRaw IS NOT INITIAL ).

      <lfs_out>-ApprovedByName     = <lfs_detail>-ApproverName.
      <lfs_out>-ApprovedByPosition = <lfs_detail>-ApproverPosition.
      <lfs_out>-ApprovedDateText   = <lfs_detail>-ApprovalDateText.
      <lfs_out>-ApprovalNoteText   = COND #( WHEN lv_approved = abap_true THEN gc_approval_note ).

    ENDLOOP.

  ENDMETHOD.


  METHOD build_items.

    LOOP AT gt_header ASSIGNING FIELD-SYMBOL(<lfs_header>).

      DATA(lv_item_number) = 0.

      LOOP AT get_active_items( <lfs_header>-PurchaseOrder ) ASSIGNING FIELD-SYMBOL(<lfs_src>).

        lv_item_number += 1.
        APPEND INITIAL LINE TO rt_item ASSIGNING FIELD-SYMBOL(<lfs_out>).

        <lfs_out>-PurchaseOrder       = <lfs_src>-PurchaseOrder.
        <lfs_out>-PurchaseOrderItem   = <lfs_src>-PurchaseOrderItem.
        <lfs_out>-ItemNumber          = lv_item_number.
        <lfs_out>-Material            = <lfs_src>-Material.
        <lfs_out>-MaterialDescription = <lfs_src>-MaterialDescription.
        " รหัสวัสดุตามด้วยคำอธิบาย
        " item ที่ไม่มีรหัสวัสดุใช้คำอธิบายอย่างเดียว
        <lfs_out>-ItemDescription     = COND #( WHEN <lfs_src>-Material IS INITIAL
                                                THEN <lfs_src>-MaterialDescription
                                                ELSE |{ <lfs_src>-Material } { <lfs_src>-MaterialDescription }| ).
        <lfs_out>-Quantity            = <lfs_src>-Quantity.
        <lfs_out>-QuantityText        = zcl_pure001_util=>format_quantity( <lfs_src>-Quantity ).
        <lfs_out>-Unit                = <lfs_src>-Unit.
        <lfs_out>-DocumentCurrency    = <lfs_src>-DocumentCurrency.
        <lfs_out>-NetPriceAmount      = <lfs_src>-NetPriceAmount.
        <lfs_out>-NetPriceQuantity    = <lfs_src>-NetPriceQuantity.
        <lfs_out>-ItemAmount          = <lfs_src>-NetAmount.
        <lfs_out>-TaxCode             = <lfs_src>-TaxCode.
        <lfs_out>-Plant               = <lfs_src>-Plant.

        "--- schedule line ให้วันส่งของ เลขที่ใบขอซื้อ และ performance period ------------
        ASSIGN gt_schedule_line[ PurchaseOrder     = <lfs_src>-PurchaseOrder
                                 PurchaseOrderItem = <lfs_src>-PurchaseOrderItem ] TO FIELD-SYMBOL(<lfs_schedule>).
        IF sy-subrc = 0.
          <lfs_out>-DeliveryDate               = <lfs_schedule>-ScheduleLineDeliveryDate.
          <lfs_out>-PerformancePeriodStartDate = <lfs_schedule>-PerformancePeriodStartDate.
          <lfs_out>-PerformancePeriodEndDate   = <lfs_schedule>-PerformancePeriodEndDate.
          <lfs_out>-PurchaseRequisition        = <lfs_schedule>-PurchaseRequisition.
        ENDIF.
        IF <lfs_out>-PurchaseRequisition IS INITIAL.
          <lfs_out>-PurchaseRequisition = <lfs_src>-PurchaseRequisition.
        ENDIF.
        " ฟอร์มใช้ dd/mm/yyyy ค.ศ. ระดับ item ตามฟอร์มมาตรฐาน
        IF <lfs_out>-DeliveryDate IS NOT INITIAL.
          <lfs_out>-DeliveryDateText = |Delivery Date : { zcl_pure001_util=>format_date_dmy( <lfs_out>-DeliveryDate ) }|.
        ENDIF.

        "--- account assignment --------------------------------------------------
        ASSIGN gt_account_assgmt[ PurchaseOrder     = <lfs_src>-PurchaseOrder
                                  PurchaseOrderItem = <lfs_src>-PurchaseOrderItem ] TO FIELD-SYMBOL(<lfs_aa>).
        IF sy-subrc = 0.
          <lfs_out>-GLAccount  = <lfs_aa>-GLAccount.
          <lfs_out>-CostCenter = <lfs_aa>-CostCenter.
          <lfs_out>-OrderID    = <lfs_aa>-OrderID.
          <lfs_out>-WBSElement = <lfs_aa>-WBSElement.
        ENDIF.

        " รูปแบบคือ PR No.: <เลข>  Acc.Code: <เลข>  Order No.: <เลข>
        " ข้ามส่วนที่ว่าง
        " ALPHA = OUT ตัดศูนย์นำหน้า แล้ว condense ตัดช่องว่างท้ายที่เหลือ
        DATA lt_part TYPE string_table.

        CLEAR lt_part.
        IF <lfs_out>-PurchaseRequisition IS NOT INITIAL.
          APPEND |PR No.: { condense( |{ <lfs_out>-PurchaseRequisition ALPHA = OUT }| ) }| TO lt_part.
        ENDIF.

        IF <lfs_out>-GLAccount IS NOT INITIAL.
          APPEND |Acc.Code: { condense( |{ <lfs_out>-GLAccount ALPHA = OUT }| ) }| TO lt_part.
        ENDIF.

        IF <lfs_out>-OrderID IS NOT INITIAL.
          APPEND |Order No.: { condense( |{ <lfs_out>-OrderID ALPHA = OUT }| ) }| TO lt_part.
        ENDIF.

        <lfs_out>-AccountAssignmentText = concat_lines_of( table = lt_part sep = `  ` ).

        " item บริการที่ไม่มีจำนวนพิมพ์เป็น 1 AU ตามฟอร์มมาตรฐาน
        " field Quantity ยังคงเป็น 0 ตามข้อมูลจริง
        IF <lfs_out>-Quantity IS INITIAL.
          <lfs_out>-QuantityText = '1'.
          <lfs_out>-Unit         = gc_service_unit.
        ENDIF.

        " ฟอร์มพิมพ์รหัสหน่วยภายใน ไม่ใช่รหัส ISO ที่ serializer แปลงให้ Unit
        <lfs_out>-UnitText = <lfs_out>-Unit.

        <lfs_out>-ItemDescriptionText = compose_item_description( is_item     = <lfs_out>
                                                                  iv_language = <lfs_header>-Language ).

      ENDLOOP.
    ENDLOOP.

  ENDMETHOD.


  METHOD build_item_texts.

    LOOP AT gt_header ASSIGNING FIELD-SYMBOL(<lfs_header>).
      LOOP AT get_active_items( <lfs_header>-PurchaseOrder ) ASSIGNING FIELD-SYMBOL(<lfs_item>).

        DATA(lt_text) = get_ordered_item_texts( iv_purchase_order      = <lfs_item>-PurchaseOrder
                                                iv_purchase_order_item = <lfs_item>-PurchaseOrderItem
                                                iv_language            = <lfs_header>-Language ).

        LOOP AT lt_text ASSIGNING FIELD-SYMBOL(<lfs_text>).
          APPEND VALUE #( PurchaseOrder     = <lfs_item>-PurchaseOrder
                          PurchaseOrderItem = <lfs_item>-PurchaseOrderItem
                          TextSequence      = sy-tabix
                          TextObjectType    = <lfs_text>-TextObjectType
                          TextTypeName      = SWITCH #( <lfs_text>-TextObjectType
                                                WHEN gc_text_type-material_po_text THEN 'Material PO Text'
                                                WHEN gc_text_type-item_text        THEN 'Item Text' )
                          Text              = <lfs_text>-PlainLongText
                        ) TO rt_text.
        ENDLOOP.

      ENDLOOP.
    ENDLOOP.

  ENDMETHOD.


  METHOD get_ordered_item_texts.

    " ลำดับตามฟอร์มมาตรฐาน คือ Material PO Text แล้วตามด้วย Item Text
    DATA(lt_type_order) = VALUE string_table( ( |{ gc_text_type-material_po_text }| )
                                              ( |{ gc_text_type-item_text }| ) ).

    LOOP AT lt_type_order INTO DATA(lv_type).
      ASSIGN gt_item_text[ PurchaseOrder     = iv_purchase_order
                           PurchaseOrderItem = iv_purchase_order_item
                           TextObjectType    = lv_type
                           Language          = iv_language ] TO FIELD-SYMBOL(<lfs_text>).

      IF sy-subrc = 0 AND <lfs_text>-PlainLongText IS NOT INITIAL.
        APPEND <lfs_text> TO rt_text.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.


  METHOD compose_item_description.

    DATA lt_line TYPE string_table.

    " บรรทัดแรกเป็นคำอธิบายรายการอย่างเดียว รหัสวัสดุอยู่บรรทัดล่างในวงเล็บ
    APPEND is_item-MaterialDescription TO lt_line.

    DATA(lt_text) = get_ordered_item_texts( iv_purchase_order      = is_item-PurchaseOrder
                                            iv_purchase_order_item = is_item-PurchaseOrderItem
                                            iv_language            = iv_language ).

    LOOP AT lt_text ASSIGNING FIELD-SYMBOL(<lfs_text>).
      APPEND <lfs_text>-PlainLongText TO lt_line.
    ENDLOOP.

    IF is_item-DeliveryDateText IS NOT INITIAL.
      APPEND is_item-DeliveryDateText TO lt_line.
    ENDIF.

    " รายการที่เป็นวัสดุจะมีรหัสต่อท้ายในวงเล็บ รายการข้อความล้วนไม่มี
    IF is_item-Material IS NOT INITIAL.
      APPEND |({ is_item-Material })| TO lt_line.
    ENDIF.

    IF is_item-AccountAssignmentText IS NOT INITIAL.
      APPEND is_item-AccountAssignmentText TO lt_line.
    ENDIF.
    IF is_item-WBSElement IS NOT INITIAL.
      APPEND |WBS: { is_item-WBSElement }| TO lt_line.
    ENDIF.

    rv_text = concat_lines_of( table = lt_line sep = cl_abap_char_utilities=>newline ).

  ENDMETHOD.


  METHOD get_active_items.

    rt_item = VALUE #( FOR ls_item IN gt_item
                       WHERE ( PurchaseOrder                  = iv_purchase_order
                           AND PurchasingDocumentDeletionCode <> 'L' )
                       ( ls_item ) ).

    SORT rt_item BY PurchaseOrderItem.

  ENDMETHOD.


  METHOD get_header_text.

    rv_text = VALUE #( gt_header_text[ PurchaseOrder  = iv_purchase_order
                                       TextObjectType = iv_text_type
                                       Language       = iv_language ]-PlainLongText OPTIONAL ).

  ENDMETHOD.

ENDCLASS.
